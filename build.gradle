plugins {
    alias libs.plugins.architectury.plugin
    alias libs.plugins.architectury.loom apply false
    alias libs.plugins.forgix
}

architectury {
    minecraft = libs.versions.minecraft
}

subprojects {
    apply plugin: "dev.architectury.loom"

    dependencies {
        minecraft libs.minecraft
        mappings variantOf(libs.yarn.mappings) { classifier "v2" }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = "${rootProject.mod_version}+mc${libs.versions.minecraft.get()}"
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

forgix {
    group = rootProject.maven_group
    mergedJarName = "${rootProject.archives_base_name}-${rootProject.version}.jar"
    outputDir = "build/libs/forgix"

    forge {
        projectName = "forge"
        jarLocation = "build/libs/${rootProject.archives_base_name}-forge-${rootProject.version}.jar"
    }

    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/${rootProject.archives_base_name}-fabric-${rootProject.version}.jar"
    }
}
